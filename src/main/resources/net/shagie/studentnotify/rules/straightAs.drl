package net.shagie.studentnotify.rules

import net.shagie.studentnotify.data.Student
import net.shagie.studentnotify.data.Semester
import net.shagie.studentnotify.data.Notification
import net.shagie.studentnotify.data.NotifyIds
import net.shagie.studentnotify.data.Grade
import java.util.*

global java.util.List notifications;

dialect "mvel"

/* Triggers every semester as long as the student maintains all As */

rule "straight As"
    when
        $s : Student(
            grades != null,
            $g : grades,
            $n : notifications
        )
        not Semester(reading != Grade.A || writing != Grade.A || math != Grade.A) from $g
        not Notification(id == NotifyIds.STRAIGHT_AS, semester == $g.size()) from $n
    then
        notifications.add(new Notification(NotifyIds.STRAIGHT_AS,
            ($s.getGrades() == null || $s.getGrades().isEmpty()) ? 0 : $s.getGrades().size(),
            "allAs " + $s.getName()));
end

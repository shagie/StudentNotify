package net.shagie.studentnotify.rules

import net.shagie.student.data.Student
import net.shagie.student.data.Semester
import net.shagie.student.data.Notification
import net.shagie.student.data.NotifyIds
import net.shagie.student.data.Grade
import java.util.*

global java.util.List newEvents;

dialect "mvel"

/* Triggers every semester as long as the student maintains all As */

rule "straight As"
    when
        $s : Student(
            grades != null,
            grades.size() > 0,
            $g : grades,
            $studentId : studentId
        )
        not Semester(reading != Grade.A || writing != Grade.A || math != Grade.A) from $g
        not Notification(
            studentId == $studentId,
            notificationType == NotifyIds.STRAIGHT_AS,
            semester == $g.size()
        )
    then
        newEvents.add(new Notification(NotifyIds.STRAIGHT_AS,
            $s.getStudentId(),
            ($s.getGrades() == null || $s.getGrades().isEmpty()) ? 0 : $s.getGrades().size(),
            "allAs " + $s.getName()));
end

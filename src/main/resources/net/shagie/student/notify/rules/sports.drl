package net.shagie.studentnotify.rules

import net.shagie.student.data.Student
import net.shagie.student.data.Semester
import net.shagie.student.data.Notification
import net.shagie.student.data.NotifyIds

global java.util.List newEvents;

dialect "mvel"

/* Sports elgibility triggers every year.
 *   - Must have all Cs or better
 *   - Must have a GPA at least 2.5
 */

rule "sports eligibility"
    when
        $s : Student(
            grades != null,
            grades.size() > 0,
            $g : grades,
            $l : grades.get(grades.size() - 1),
            $studentId : studentId
        )
        not Notification(
            studentId == $studentId,
            notificationType == NotifyIds.SPORTS_ELIGIBILITY,
            semester == $g.size()
        )
        Semester ( ((reading.score + writing.score + math.score) / 3) > 2.5 ) from $l
        Semester ( reading.score > 1, writing.score > 1, math.score > 1 ) from $l
    then
        newEvents.add(new Notification(NotifyIds.SPORTS_ELIGIBILITY,
            $s.getStudentId(),
            ($s.getGrades() == null || $s.getGrades().isEmpty()) ? 0 : $s.getGrades().size(),
            "sports eligibility " + $s.getName()));
end
